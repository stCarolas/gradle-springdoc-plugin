apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "signing"

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.springframework:spring-webmvc:4.0.0.RELEASE'

    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
}

repositories {
    mavenCentral()
}

sourceSets {
    generated
}

ext {
    SONATYPE_USER = System.env.SONATYPE_USER
    SONATYPE_PASSWORD = System.env.SONATYPE_PASSWORD
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: "$SONATYPE_USER", password: "$SONATYPE_PASSWORD")
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: "$SONATYPE_USER", password: "$SONATYPE_PASSWORD")
            }

            pom.project {
                name "gradle-springdoc-plugin"
                packaging "jar"
                description "A gradle plugin to generate html rest api doc from spring mvc annotations."
                url "http://github.com/thilko/gradle-springdoc-plugin"

                scm {
                    url "https://github.com/thilko/gradle-springdoc-plugin.git"
                    connection "https://github.com/thilko/gradle-springdoc-plugin.git"
                    developerConnection "https://github.com/thilko/gradle-springdoc-plugin.git"
                }

                licenses {
                    license {
                        name "MIT"
                        url "http://opensource.org/licenses/MIT"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "thilko"
                        name "Thilko Richter"
                        email "springdoc@thilko.com"
                        timezone "+1"
                        properties {
                            picUrl "http://www.gravatar.com/avatar/6682c3909950f44d635929ef21ec08c8.png"
                        }
                    }
                }
                groupId = 'com.thilko.spring'
                artifactId = 'gradle-springdoc-plugin'
                version = "0.2.SNAPSHOT"
            }
        }
    }
}
