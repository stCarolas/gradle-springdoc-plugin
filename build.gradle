apply plugin: "groovy"
apply plugin: "maven"

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.springframework:spring-webmvc:4.0.0.RELEASE'

    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
}

repositories {
    mavenCentral()
}

sourceSets {
    generated
}

ext {
    SONATYPE_USER = System.env.SONATYPE_USER
    SONATYPE_PASSWORD = System.env.SONATYPE_PASSWORD
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: "$SONATYPE_USER", password: "$SONATYPE_PASSWORD")
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: "$SONATYPE_USER", password: "$SONATYPE_PASSWORD")
            }

            pom {
                groupId = 'com.thilko.spring'
                artifactId = 'gradle-springdoc-plugin'
                version = "0.1.SNAPSHOT"

                withXml {
                    asNode().appendNode("name", "gradle-springdoc-plugin")
                    asNode().appendNode("description", "A gradle plugin to generate html rest api doc from spring mvc annotations.")
                    asNode().appendNode("packaging", "jar")
                    asNode().appendNode("url", "http://github.com/thilko/gradle-springdoc-plugin")
                    def license = asNode().appendNode("licenses").appendNode("license")
                    license.appendNode("name", "MIT")
                    license.appendNode("url", "http://opensource.org/licenses/MIT")
                    license.appendNode("distribution", "repo")
                    def scm = asNode().appendNode("scm")
                    scm.appendNode("url", "http://github.com/thilko/gradle-springdoc-plugin")
                    def developers = asNode().appendNode("developers").appendNode("developer")
                    developers.appendNode("name", "Thilko Richter")
                    developers.appendNode("email", "springdoc@thilko.com")
                    developers.appendNode("timezone", "+1")
                    developers.appendNode("properties").appendNode("picUrl", "http://www.gravatar.com/avatar/6682c3909950f44d635929ef21ec08c8.png")
                }
            }
        }
    }
}
